import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'maven-publish'
}

rootProject.ext.mavenUserName=""
rootProject.ext.mavenPassword=""

if (file("./build.gradle.local").exists()) {
    apply from: "./build.gradle.local"
}

repositories {
    mavenLocal()
    mavenCentral()

    maven { url 'https://papermc.io/repo/repository/maven-public/' }
}

dependencies {
    compileOnly 'com.velocitypowered:velocity-api:3.1.2-SNAPSHOT'
}

group = 'uk.co.notnull'
version = '1.2-SNAPSHOT'
description = 'Vivecraft velocity extensions'

java.sourceCompatibility = JavaVersion.VERSION_11
java.targetCompatibility = JavaVersion.VERSION_11

tasks {
    processResources {
        from("${project.rootDir}/src/main/resources") {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
            project.properties.findAll().each {
                prop ->
                    if (prop.value != null && prop.value instanceof java.lang.String) {
                        filter(ReplaceTokens, tokens: [(prop.key): prop.value])
                        filter(ReplaceTokens, tokens: [('project.' + prop.key): prop.value])
                    }
            }
        }
    }

    compileJava {
        options.compilerArgs += ["-Xlint:all", "-Xlint:-processing"]
        options.encoding = 'UTF-8'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'VivecraftVelocityExtensions'
                description = 'Velocity support plugin for Vivecraft Spigot Extensions'
                url = 'https://github.com/JLyne/VivecraftVelocityExtensions'
                developers {
                    developer {
                        id = 'jim'
                        name = 'James Lyne'
                    }
                    developer {
                        id = 'techjar'
                        name = 'Techjar'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/JLyne/VivecraftVelocityExtensions.git'
                    developerConnection = 'scm:git:ssh://github.com/JLyne/VivecraftVelocityExtensions.git'
                    url = 'https://github.com/JLyne/VivecraftVelocityExtensions'
                }
            }
        }
    }
    repositories {
        maven {
            credentials {
                username = project.ext.mavenUserName
                password = project.ext.mavenPassword
            }
            authentication {
                basic(BasicAuthentication)
            }

            def releasesRepoUrl = "https://repo.not-null.co.uk/releases/" // gradle -Prelease publish
            def snapshotsRepoUrl = "https://repo.not-null.co.uk/snapshots/"
            url = project.hasProperty('release') ? releasesRepoUrl : snapshotsRepoUrl
        }
    }
}
